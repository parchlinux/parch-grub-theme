# Maintainer: Komeil Parseh <ahmdparsh129 at gmail dot com>

pkgname=(
    parch-grub-dark-git
    parch-grub-light-git)
pkgbase=parchlinux-grub-theme-git
_pkg="${pkgbase%-git}"
pkgver=r2.73c76a8
pkgrel=1
pkgdesc='Parchlinux theme for GRUB2'
arch=('any')
url='https://github.com/parchlinux/parch-grub-theme'
license=('MIT')
makedepends=('git')
source=("$_pkg::git+$url")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkg}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_package() {
	cd "${_pkg}"
	install -d "$pkgdir/usr/share/grub/themes/"
	cp -a --no-preserve=ownership "${pkgname%-git}" "$pkgdir/usr/share/grub/themes/${pkgname%-git}"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_parch-grub-dark-git() {
	pkgdesc+=' -- Dark'
	install=$pkgname.install

	_package parch-grub-dark
}

package_parch-grub-light-git() {
	pkgdesc+=' -- Light'
	install=$pkgname.install

	_package parch-grub-light
}
